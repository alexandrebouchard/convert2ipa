#### http://en.wikipedia.org/wiki/Portuguese_orthography

#### EUROPEAN PORTUGUESE IPA CONVERTER

cons := [cdfghjklmnpqrstvwxyz]
consfinal := [cdfghjklmnpqrstvwxyz_]
vow := [aáàâäeéèêëiíìîïoóòôöuúùûü]
letter := [cdfghjklmnpqrstvwxyzaáàâäeéèêëiíìîïoóòôöuúùûü]

a := [aáàâä]
e := [eéèêë]
i := [iíìîï]
o := [oóòôö]
u := [uúùûü]
ei := [eéèêëiíìîï]

unvoiced := [cfhkpqst]
voiced := [bdgjlmnrvwz]

# stress is handle approximately with these two conjunctions
# TODO: there are exceptions
stressed1 := syllable=-1,endsWithVowel=false
stressed2 := syllable=-2,endsWithVowel=true

# Diacritics --- easy

á -> a
â -> ɐ
ã -> A    # TODO: actually, nasalized: ɐ̃
à -> a
ç -> s
é -> ɛ
ê -> e
í -> i
ó -> ɔ
ô -> o
õ -> O    # TODO: actually, nasalized: õ
ú -> u
ü -> u

# Some important digraphs

ch -> ʃ
lh -> ʎ
nh -> ɲ
rr -> ʁ
ss -> s
sç -> s
xc -> s

# conditional digraphs  TODO:  using capitals to denote nasalized vowels

/{an,am,ân,âm}/$cons -> A  # TODO: actually, ɐ̃
$letter/am/_ -> AU    # TODO: actually, ɐ̃ũ
/{en,em,ên,êm}/$cons -> E  # TODO: actually, ẽ
$letter/{em,ém}/_ -> EI  # TODO: actually, ẽĩ ~ ɐ̃ĩ
$letter/{en,én}/s_ -> EI  # TODO: actually, ẽĩ ~ ɐ̃ĩ
$letter/êm/_ -> EI   # TODO: actually ẽĩẽĩ ~ 'ɐ̃ĩɐ̃ĩ ~ ẽĩ
/gu/$ei -> g
/gu/ -> gu
/{in,im,ín,ím}/$consfinal -> I  # TODO: actually, ĩ
/{on,om,ôn,ôm}/$consfinal -> O   # TODO: actually, õ

/qu/$ei -> k
/qu/ -> ku
/{un,um,ún,úm}/$consfinal -> U   # TODO: actually, ũ

# Diphtongs 
# can generally be analysed as hiatuses, except ei
ei -> ɐi

# Main orthographical rules

# A
a -> a when $stressed1  # TODO: can actually be ɐ in some contexts
a -> a when $stressed2  # TODO: can actually be ɐ in some contexts
# unstressed
a -> ɐ

# B
b -> b 

# C
/{sc,c}/$ei -> s
c -> k

# D
d -> d

# E 
e -> e when $stressed1   # TODO: can actually be ɛ in some contexts
e -> e when $stressed2   # TODO: can actually be ɛ in some contexts
# unstressed
e -> ɨ  				# TODO: actually, a Near-close near-back unrounded vowel, 
                    #       but no std unicode support for that (sometimes ɯ̽)

# F 
f -> f 

# G
/g/$ei -> ʒ
g -> g

# H
h -> 

# I
i -> i

# J
j -> ʒ

# K 
# foreign only
k -> k

# L
l -> l

# M
m -> m

# N
n -> n

# O
o -> o when $stressed1  # TODO: can actually be ɔ in some contexts
o -> o when $stressed2  # TODO: can actually be ɔ in some contexts
# unstressed
o -> u

# P
p -> p

# Q
q -> k

# R : TODO: approximation, should be begining and end of syllable
_/r/ -> ʁ
r -> ɾ

# S
_/s/ -> s
$vow/s/$vow -> z
$cons/s/ -> s
# In most of Portugal, and in Rio de Janeiro and some northeastern states of 
# Brazil, syllable-final sibilants have become postalveolar, /ʃ/ before a 
# voiceless consonant or at the end of a word, and /ʒ/ before a voiced 
# consonant: isto /'iʃtu/, turismo /tu'riʒmu/.
/s/_ -> ʃ  
/s/$unvoiced -> ʃ
/s/$voiced -> ʒ
s -> s

# T	
t -> t

# U
u -> u

# V	  (brazilian portuguese only)																	
v -> v  			

# W  (foreign only)
w -> u

# X
$vow/x/$vow -> z
_/x/ -> ʃ
/x/$i -> ks
x -> s

# Z
z -> z 